plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'de.undercouch.download' version "4.1.1"
    id 'com.google.osdetector' version '1.6.2'
    id 'java'
    id 'idea'
}

allprojects {
    /* Group name of our artifacts */
    group = 'org.vitrivr'

    /* Our current version */
    version = '3.0.3-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
}

subprojects {
    buildscript {
        repositories {
            jcenter()
            mavenCentral()
            maven {
                url "https://plugins.gradle.org/m2"
            }
        }

        dependencies {
            classpath "gradle.plugin.com.google.gradle:osdetector-gradle-plugin:1.6.2"
            classpath "gradle.plugin.com.google.protobuf:protobuf-gradle-plugin:0.8.14"
            classpath "de.undercouch:gradle-download-task:4.1.1"
        }
    }

    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    /* Important warnings that are currently missing simply because there are too many: "cast", "rawtypes". Ideally, "all" should be used in the future. */
    def enabledWarnings = ["-Xlint:deprecation", "-Xlint:empty", "-Xlint:overrides", "-Xlint:serial", "-Xlint:static", "-Xlint:unchecked", "-Xlint:varargs"]
    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs += enabledWarnings
    }
    compileTestJava.options.compilerArgs += enabledWarnings

    dependencies {
        /** Log4j 2 */
        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: version_log4j2
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: version_log4j2
        implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: version_log4j2

        /** Jackson (JSON conversion) */
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: version_jackson
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: version_jackson

        /** Test dependencies (JUnit 5) */
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: version_junit
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: version_junit
        testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: version_junit_platform
    }
}

project(':cineast-runtime') {
    dependencies {
        implementation project(path: ':cineast-core', configuration: 'shadow')
    }
}

project(':cineast-api') {
    dependencies {
        implementation project(path: ':cineast-runtime', configuration: 'shadow')
        implementation project(path: ':cineast-core')
    }
}

idea {
    module {
        downloadJavadoc = true

        excludeDirs += file('data')
        excludeDirs += file('thumbnails')
        excludeDirs += file('output')
    }
}

task getExternalFiles {
    doLast {
        def fileList = new File("externalFiles.csv")
        fileList.eachLine { String line ->
            def split = line.split(",")
            download {
                src split[0]
                dest split[1]
            }
        }
    }
}

task generateOpenApiSpecs(type: JavaExec){
    classpath = project(":cineast-api").sourceSets.main.runtimeClasspath
    main = 'org.vitrivr.cineast.api.docs.GenerateOpenApiSpecs'

    def config = project.hasProperty("cineastConfig") ? project.getProperty("cineastConfig") : "cineast.json"

    args("${config}")
}

